(macro join (glue arr)
       ("(" (translate arr) ").join(" (translate glue) ")"))

(macro set (arr ...kv-pairs)
       (join "\n" (bulk-map kv-pairs
                            (#(k v)
                              ("(" (translate arr) ")"
                                   "[" (translate k) "] = " (translate v) ";")))))

(macro get (obj ...keys)
       ("(" (translate obj) ")"
            (join "" (map keys (#(key)
                                 ("[" (translate key) "]"))))))

(macro var (...pairs)
       (as-statement
        ("var "
         (join ",\n    "
               (bulk-map pairs
                         (#(name value)
                           (concat (translate name) " = "
                                   (translate value)))))
         ";")))

(macro alias-macro (current-macro-name desired-macro-name)
       (var current-macro-name (translate current-macro-name)
            desired-macro-name (translate desired-macro-name))
       (set macros desired-macro-name (get macros current-macro-name))
       "")

(macro send (object method ...args)
       (concat (translate object) "." (translate method)
               "(" (join ", " (map args translate)) ")"))

(macro apply (fn arglist)
       (^send fn 'apply 'undefined arglist))

(macro list (...args)
       ("[ " (join ", " (map args translate)) " ]"))

(macro cons (first rest)
       (^send (^list first) 'concat rest))

(macro append (list ...additional)
       (^send list 'concat (apply ^list additional)))


(macro length (arr)
       (^get arr "\"length\""))

(macro ternary (cond if-true if-false)
       ("(" (translate cond) ") ? "
            (translate if-true) " : "
            (translate if-false)))

(macro first (arr) (^get arr 0))
(macro second (arr) (^get arr 1))
(macro third (arr) (^get arr 2))
(macro fourth (arr) (^get arr 3))
(macro fifth (arr) (^get arr 4))
(macro sixth (arr) (^get arr 5))
(macro seventh (arr) (^get arr 6))
(macro eighth (arr) (^get arr 7))
(macro ninth (arr) (^get arr 8))

(macro rest (arr)
       (^send arr 'slice 1))

(macro last (arr)
       (^get (^send arr 'slice -1) 0))

(macro = (a b) (concat (translate a) " === " (translate b)))
(macro +   (...args)
       ("(" (join " + " (map args translate)) ")"))
(macro -   (...args)
       ("(" (join " - " (map args translate)) ")"))
(macro *   (...args)
       ("(" (join " * " (map args translate)) ")"))
(macro /   (...args)
           ("(" (join " / " (map args translate)) ")"))
(macro or  (...args)
       ("(" (join " || " (map args translate)) ")"))

(macro and (...args)
       (ternary (= 1 (length args)) (translate (first args))
                ("(" (join " && " (map args translate)) ")")))
(macro mod (...args)
       ("(" (join " % " (map args translate)) ")"))

(macro infix-comparator (comparator args)
       ("(" (join " && "
                  (map (args.slice 0 -1)
                       (#(item index)
                         (concat item
                                 " " comparator " "
                                 (get args (+ 1 index))))))
            ")"))

(macro >  (...args) (^infix-comparator  ">"  (map args translate)))
(macro <  (...args) (^infix-comparator  "<"  (map args translate)))
(macro <= (...args) (^infix-comparator "<="  (map args translate)))
(macro >= (...args) (^infix-comparator ">="  (map args translate)))
(macro != (...args) (^infix-comparator "!==" (map args translate)))

(macro pow (base exponent)
       (^call "Math.pow" base exponent))

(macro incr-by (item increment)
       (concat (translate item) " += " (translate increment)))

(macro incr (item)
       ("((" (translate item) ")++)"))

(macro decr (item)
       ("((" (translate item) ")--)"))

(macro new (...args)
       ("(new " (apply ^call args) ")"))

(macro regex (string glim)
       (call (get macros 'new) 'RegExp string (or glim 'undefined)))

(macro timestamp ()
       ("\"" (send (new Date) to-string) "\""))

(macro comment (...contents)
       (map contents
            (#(item)
              (join "\n" (map (send (translate item) split "\n")
                              (#(line) ("// " line)))))))

(macro meta (body)
       (eval (translate body)))

(macro zero? (item)
       ((get macros "=") (translate item) 0))

(macro empty? (arr)
       ("((" (translate arr) ").length === 0)"))

(macro odd? (number)
       ((get macros "!=") 0
        (^mod (translate number) 2)))

(macro even? (number)
       ((get macros "=") 0
        (^mod (translate number) 2)))


(macro typeof (thing) (+ "typeof " (translate thing)))

(macro function? (...things)
       (apply ^and (map things (#(thing)
                                       ((get macros "=") (^typeof thing) "'function'")))))

(macro undefined? (...things)
       (apply ^and (map things (#(thing)
                                       ((get macros "=") (^typeof thing) "'undefined'")))))

(macro defined? (...things)
       (apply ^and (map things (#(thing)
                                       ((get macros "!=") (^typeof thing) "'undefined'")))))

(macro number? (...things)
       (apply ^and (map things (#(thing)
                                       ((get macros "=") (^typeof thing) "'number'")))))

(macro if (arg truebody falsebody)
       ("(function() {"
        (indent ("if (" (translate arg) ") {"
                        (indent (^do truebody))
                        "} else {"
                        (indent (^do falsebody))
                        "}"))
        "})()"))

(macro = (first-thing ...other-things)
       (var translated-first-thing (translate first-thing))
       ("(" (join " &&\n "
                  (map other-things
                       (#(thing)
                         (concat translated-first-thing
                                 " === "
                                 (translate thing)))))
            ")"))


(macro string? (thing)
       ("typeof(" (translate thing) ") === \"string\""))

(macro list? (thing)
       (var translated ("(" (translate thing) ")"))
       (concat translated " && "
               translated ".constructor.name === \"Array\""))


(macro when (arg ...body)
       ("(function() {"
        (indent ("if (" (translate arg) ") {"
                        (indent (apply ^do body))
                        "}"))
        "})()"))

(macro unless (condition ...body)
       (apply ^when (cons ['not condition] body)))

(macro not (exp)
       ("(!" (translate exp) ")"))

(macro slice (arr start end)
       (^send (translate arr) "slice" start end))

(macro inspect (...args)
       (join " + \"\\n\" + "
             (map args
                  (#(arg)
                    ("\"" arg ":\" + " (translate arg))))))

(macro each (item array ...body)
       (^send (translate array) 'for-each
                    (apply ^lambda (cons item body))))


(macro setf (...args)
       (join "\n"
             (bulk-map args (#(name value)
                              (as-statement (concat (translate name) " = "
                                                    (translate value)))))))
(alias-macro setf assign)


(macro macro-list ()
       ("[" (indent (join ",\n"
                          (map (-object.keys macros)
                               ^quote)))
            "]"))

(macro macroexpand (name)
       (var macro (get macros (translate name)))
       (if macro
           ("// macro: " name "\n" (send macro to-string))
           "undefined"))

(macro throw (...string)
       ("throw new Error (" (join " " (map string translate)) ")"))

(macro as-boolean (expr)
       ("(!!(" (translate expr) "))"))

(macro chain (object ...calls)
       (if (= 0 calls.length) (translate object)
           (if (= 1 calls.length) (apply ^send (cons object (first calls)))
               (concat (translate object)
                       (indent (join "\n"
                                     (map calls (#(call index)
                                                  ("." (translate (first call))
                                                       "(" (join ", " (map (rest call) translate)) ")")))))))))

(macro chainable (...names)
       (each (name) names
             (^macro name ['target "..." 'calls]
                           ['apply 'macros.chain ['cons ['macros.call ['quote name] 'target] 'calls]])))

(macro try (tryblock catchblock)
       ("(function() {"
        (indent ("try {"
                 (indent (^do tryblock))
                 "} catch (e) {"
                 (indent (^do catchblock))
                 "}"))
        "})()"))


(macro while (condition ...block)
       (^scoped
        (^var '**return-value**)
        ("while (" (translate condition) ") {"
                   (indent (^setf '**return-value**
                                        (apply ^scoped block))))
        "}"
        '**return-value**))

(macro until (condition ...block)
       (apply (get macros 'while)
              (cons ['not condition] block)))


(macro thunk (...args)
       (apply ^lambda (cons [] args)))
(alias-macro thunk #>)

(macro keys (obj)
       (^call "Object.keys" (translate obj)))

(macro delete (...objects)
       (join "\n" (map objects (#(obj)
                                 ("delete " (translate obj) ";")))))

(macro delmacro (macro-name)
       (delete (get macros (translate macro-name)))
       "")
(alias-macro delmacro delete-macro)


(macro rename-macro (current-macro-name desired-macro-name)
       (^alias-macro current-macro-name desired-macro-name)
       (^delmacro current-macro-name)
       "")

(macro defhash (name ...pairs)
       (^var name (apply ^hash pairs)))

(macro arguments ()
       "(Array.prototype.slice.apply(arguments))")

(macro scoped (...body)
       (^call (apply ^thunk body)))

(macro each-key (as obj ...body)
       ("(function() {"
        (indent
         ("for (var " (translate as) " in " (translate obj) ") "
                      (apply ^scoped body)
                      ";"))
        "})();"))

(macro match? (regexp string)
       (^send string 'match regexp))

(macro switch (obj ...cases)

       ;; the complexity of this macro indicates there's a problem
       ;; I'm not quite sure where to fix this, but it has to do with quoting.
       (var lines (list ("switch(" (translate obj) ") {")))
       (each (case-def) cases
             (var case-name (first case-def))
             (when (and (list? case-name)
                        (= (first case-name) 'quote))
                   (var second (second case-name))
                   (setf case-name (if (list? second)
                                       (map second ^quote)
                                       (^quote second))))

             (var case-string
                  (if (list? case-name)
                      (join "\n" (map case-name (#(c)
                                                  ("case " (translate c) ":"))))
                      (if (= 'default case-name) "default:"
                          ("case " (translate case-name) ":"))))

             (lines.push (concat case-string
                                 (indent (apply ^do (case-def.slice 1))))))

       ;; the following two lines are to get the whitespace right
       ;; this is necessary because switches are indented weird
       (set lines (- lines.length 1)
            (chain (get lines (- lines.length 1)) (concat "}")))

       ("(function() {" (apply indent lines) "})()"))


(macro if-else (...args)
       ("(function() {"
        (indent
         (join " else "
               (bulk-map args
                         (#(cond val)
                           (if (undefined? val)
                               ("{" (indent (^do cond)) "}")
                               ("if (" (translate cond) ") {"
                                       (indent (^do val))
                                       "}"))))))
        "})()"))

(macro let (args body)
       ("let (" (bulk-map args
                          (#(h k v)
                            (concat (translate k) " = " (translate v))))
                (if (undefined? body) ");"
                    (") {" (indent (translate body)) "}"))))

(macro instance-of? (item type)
       ("(" (translate item) " instanceof " (translate type) ")"))

(macro slice (list begin end)
       ("Array.prototype.slice.call(" (translate list)
                                      ", " (or (translate begin) 0)
                                      (if (defined? end) (", " (translate end) ")") ")")))

(macro includes? (list item)
       (call (get macros "!=") -1 (^send list 'index-of item)))

(macro excludes? (list item)
       (call (get macros "=") -1 (^send list 'index-of item)))

(macro exists? (thing)
       (call (get macros "and")
             (^defined? thing)
             (call (get macros "!=") thing 'null)))

(macro cascade (...commands)
       (translate (inject []
                          (commands.reverse)
                          (#(output cmd)
                            (if (empty? output) [cmd]
                                [cmd ['thunk output]))))))

(macro state (...pairs)
       (unless (exists? ^state.data)
               (set ^state 'data {}))

       (if (= 1 (length pairs))
           (get ^state.data (translate (first pairs)))
           (do (bulk-map pairs (#(k v) (set ^state.data (translate k) (^meta v))))
               "")))

(macro with-state (k v ...body)
       (var before (^state k))
       (^state k v)
       (var return-value
            (join "\n" (map body translate)))
       (^state k before)
       return-value)

(macro join (glue arr)
       (if (and (defined? glue) (undefined? arr))
             (assign arr glue glue undefined))
       ("(" (translate arr) ").join(" (if glue (translate glue) "\"\"") ")"))
